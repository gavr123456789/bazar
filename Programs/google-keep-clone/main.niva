Project use: "kotlinx.serialization"

@Serializable
type Note
    id: Int?              // we dont know it when creating new one
    created_at: Long      // timestamp 
    updated_at: Long      // timestamp 
    title: String
    content: String
    color: String
    tags: List::String

type IdToNode = Map(String, String)

// simple ping
path = "/hi" bind: Method.GET
route = path to: [
    Response status: Status.OK,
             body: "Hello, from niva notes server!"
]

// get all notes
pathGetAllNotes = "/getNotes" bind: Method.GET
routeGetAllNotes = pathGetAllNotes to: [
    "getNotes" log 
    result = (NotesDB readNotes)
    Response status: Status.OK, body: result
]

// save note
pathSaveNote = "/saveNote" bind: Method.POST
routeSaveNote = pathSaveNote to: [
    payload = it body strPayload
    ("save note " + payload) log  

    note = Json::Note decode: it body strPayload
    NotesDB saveNote: note
    Response status: Status.OK, body: "added $note"
]

// delete note by id
pathDeleteNote = "/deleteNote" bind: Method.POST
routeDeleteNote = pathDeleteNote to: [
    idMap = Json::IdToNode decode: it body strPayload
    noteId = idMap at: "id", unpackOrPANIC toInt
    NotesDB deleteNoteById: noteId
    Response status: Status.OK, body: "deleted id=$noteId"
]

runServer = [
    NotesDB createTables
    routes = Router routes: {route routeGetAllNotes routeSaveNote routeDeleteNote}
    routes asServer: (SunHttp port: 9000), start

    "hosting on http://localhost:9000" log
    "current routes:\n$routes" log
] 


// testt = [
//     str = """{"title":"tt","content":"pp","color":"#FBBC04"}"""
//     x = Json::Note decode: str
//     x echo
// ] do
