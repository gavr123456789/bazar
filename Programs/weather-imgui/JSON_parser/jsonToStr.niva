union JsonValue =
| JObject map: Map(String, JsonValue)
| JArray  list: {JsonValue}
| JString value: String
| JNumber value: Double
| JBool   value: Bool
| JNull

JsonValue toJsonStr -> String = | this
| JString => value packForJson
| JNumber => value toString
| JBool => value toString
| JNull =>  "null"
| JArray => this arrToJson
| JObject => this objectToJson



JArray arrToJson -> String = [

  createArr = [
    b = StringBuilder new
    list forEachIndexed: [ i, it ->
      b append: it toJsonStr
      i + 1 != list count => b append: ", "
    ]
    b toString
  ]

  ^createArr do surroundWith: "[ " and: " ]"
]

JObject objectToJson -> String = [

  createObj = [
    b = StringBuilder new
    mut i = 0
    map forEach: [ k, v ->

      b append: k packForJson,
        append: ": ",
        append: v toJsonStr
      i + 1 != map count => b append: ", "

      i <- i inc
    ]
    b toString
  ]

  ^createObj do surroundWith: "{ " and: " }"
]
